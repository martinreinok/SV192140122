IMPORTS{
	import gradeAdministration.Course;
	import gradeAdministration.Main;
	import gradeAdministration.Student;
}

GLOBAL{
	EVENTS{
		addGrade(Course c, int grade) = {c.addGrade(arg1, grade)}
	}

	PROPERTY prop1 {
		STATES{
			BAD{badGrade }
			NORMAL{okGrade}
			STARTING{noGrade {System.out.println("Started");}}
		}
		
		TRANSITIONS{
			noGrade -> badGrade [addGrade() \ grade > 9 || grade < 2 \ System.out.println("Grade error: " + grade + " Course code: " + c.getCourseCode());]
			okGrade -> badGrade [addGrade() \ grade > 9 || grade < 2 \ System.out.println("Grade error: " + grade + " Course code: " + c.getCourseCode());]
			noGrade -> okGrade [addGrade() \\ System.out.println("Grade Added: " + grade + " Course code: " + c.getCourseCode());]
			okGrade -> okGrade [addGrade() \\ System.out.println("Grade Added: " + grade + " Course code: " + c.getCourseCode());]
		}
	}

	FOREACH (Student s) {
		EVENTS {
			addCredits(Student s1, double credits) = {s1.addCredits(credits)} where {s = s1;}
				
			setCredits(Student s1, double credits) = {s1.setCredits(credits)} where {s = s1;}
				
			changeCredits() = {addCredits|setCredits}

		}

		PROPERTY prop2 {
			STATES {
				BAD {badState}
				STARTING {okCredits {System.out.println("Started");}}
			}

			TRANSITIONS {
				okCredits -> badState [changeCredits() \ credits < 0 \ System.out.println("Bad Credits! Credits less than 0. Amount: " + credits + " Student: " + s);]
				okCredits -> okCredits [changeCredits() \ credits >= 0 \ System.out.println("Credits amount: " + credits + " Student: " + s);]
			}
		}
	}
	
}
